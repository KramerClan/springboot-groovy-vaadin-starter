buildscript {
    repositories {
        jcenter()
        maven { url 'http://maven.vaadin.com/vaadin-addons' }
    }
    dependencies {
	classpath 'fi.jasoft.plugin:gradle-vaadin-plugin:0.8'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.5.RELEASE'
    }
}

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'vaadin'
// apply from: 'http://plugins.jasoft.fi/vaadin.plugin'
apply plugin: 'spring-boot'

repositories {
    jcenter()
    maven { url 'http://maven.vaadin.com/vaadin-addons' }
    mavenLocal()
}

vaadin {
	// widgetset 'app'
	vaadin.manageDependencies = false
}

project.convention.getPlugin(WarPluginConvention).webAppDirName = 'build/resources'
bootRepackage.dependsOn = [jar] // restore the deps of spring-boot
assemble.dependsOn = [bootRepackage, jar]

sourceCompatibility = 1.7
targetCompatibility = 1.7

tasks.withType(GroovyCompile) {
	groovyOptions.optimizationOptions.indy = true
}

mainClassName = "app.Application"

jar {
    dependsOn vaadinCompileWidgetset, vaadinCompileThemes
    baseName = 'app'
    // version = '0.1.0'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

project.codenarc.toolVersion = '0.21'
project.codenarc.configFile = new File('config/codenarc/ruleset.groovy')

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.+:indy'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'org.hibernate:hibernate-validator:5.1.1.Final'
    compile 'org.vaadin.spring:spring-boot-vaadin:0.0.2-SNAPSHOT'
    def vaadinVersion =  "7.2.6"
    compile "com.vaadin:vaadin-shared:${vaadinVersion}"
    compile "com.vaadin:vaadin-server:${vaadinVersion}"
    compile "com.vaadin:vaadin-client:${vaadinVersion}"
    compile "com.vaadin:vaadin-themes:${vaadinVersion}"
    compile "com.vaadin:vaadin-client-compiled:${vaadinVersion}"
}

