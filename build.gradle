buildscript {
    ext {
        gradleVaadinPluginVersion = '1.1.2'
        springBootVersion = '1.5.1.RELEASE'
        springLoadedVersion = '1.2.6.RELEASE'
    }
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "fi.jasoft.plugin:gradle-vaadin-plugin:$gradleVaadinPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.springframework:springloaded:$springLoadedVersion"
    }
}

ext {
    ext {
        groovyVersion = '2.4.8'
        gradleWrapperVersion = '3.3'
        vaadinVersion = '7.7.6'
        vaadinSpringVersion = '1.0.0'
        viritinVersion = '1.54'
        codenarcVersion = '0.25.1'
        spockVersion = '1.0-groovy-2.4'
    }
}

apply plugin: 'idea'
apply from: 'gradle/idea.gradle'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'fi.jasoft.plugin.vaadin'
apply plugin: 'org.springframework.boot'
apply from: 'gradle/springboot-vaadin-fixes.gradle'

repositories {
    jcenter()
    maven { url 'http://maven.vaadin.com/vaadin-addons' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileGroovy {
	groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
}

mainClassName = "app.Application"

jar {
    baseName = 'app'
    // version = '0.1.0'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

vaadin {
	vaadin.version = vaadinVersion
	vaadinCompile.widgetset jar.baseName
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

project.codenarc {
	toolVersion = codenarcVersion
	configFile = new File('config/codenarc/ruleset.groovy')
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile 'org.springframework.boot:spring-boot-starter-web'
    // always exclude com.vaadin packages from deps
    compile("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")
    compile("org.vaadin:viritin:${viritinVersion}") { exclude group: 'com.vaadin' }
    testCompile "org.spockframework:spock-core:${spockVersion}"
    // vaadin addons are put automatically into the widgetset 
    // compile('org.vaadin.addons:ckeditor-wrapper-for-vaadin:7.9.3') { exclude group:'com.vaadin' }
}

// Exclude all deps, that pull in newer versions of the critical libraries for
// widgetset compilation here (e.g. validation-api).  If there are errors
// building the widgetset, check with `./gradlew dependencies` first.
configurations {
	'vaadin-client' {
		resolutionStrategy {
			// XXX does not work at all.  we end up with 1.1
			failOnVersionConflict()
			force 'javax.validation:validation-api:1.0.0.GA'
			forcedModules = ['javax.validation:validation-api:1.0.0.GA',]
		}
	}
}
