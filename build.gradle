buildscript {
    ext {
        gradleVaadinPluginVersion = '0.10.1'
        springBootVersion = '1.2.6.RELEASE'
        springLoadedVersion = '1.2.4.RELEASE'
    }
    repositories.jcenter()
    dependencies {
        classpath "fi.jasoft.plugin:gradle-vaadin-plugin:$gradleVaadinPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.springframework:springloaded:$springLoadedVersion"
    }
}

ext {
    ext {
        groovyVersion = '2.4.4'
        gradleWrapperVersion = '2.7'
        vaadinVersion = '7.5.5'
        vaadin4springVersion = '0.0.5.RELEASE'
        viritinVersion = '1.33'
        codenarcVersion = '0.23'
        spockVersion = '1.0-groovy-2.4'
    }
}

apply plugin: 'idea'
apply from: 'gradle/idea.gradle'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'fi.jasoft.plugin.vaadin'
apply plugin: 'spring-boot'
apply from: 'gradle/springboot-vaadin-fixes.gradle'

repositories {
    jcenter()
    maven { url 'http://maven.vaadin.com/vaadin-addons' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileGroovy {
	groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
}

mainClassName = "app.Application"

jar {
    baseName = 'app'
    // version = '0.1.0'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

vaadin {
	vaadin.version = vaadinVersion
	widgetset jar.baseName
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

project.codenarc {
	toolVersion = codenarcVersion
	configFile = new File('config/codenarc/ruleset.groovy')
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile 'org.springframework.boot:spring-boot-starter-web'
    // always exclude com.vaadin packages from deps
    compile("org.vaadin.spring:spring-boot-vaadin:${vaadin4springVersion}") { exclude group: 'com.vaadin' }
    compile("org.vaadin:viritin:${viritinVersion}") { exclude group: 'com.vaadin' }
    testCompile "org.spockframework:spock-core:${spockVersion}"
    // vaadin addons are put automatically into the widgetset 
    // compile('org.vaadin.addons:ckeditor-wrapper-for-vaadin:7.9.3') { exclude group:'com.vaadin' }
}

// Exclude all deps, that pull in newer versions of the critical libraries for
// widgetset compilation here (e.g. validation-api).  If there are errors
// building the widgetset, check with `./gradlew dependencies` first.
configurations {
	'vaadin-client' {
		exclude module: 'spring-boot-starter-web'
		exclude module: 'spring-boot-vaadin'
	}
}
